# SD Systems CP/M 2.2 test for SIMH/AltairZ80
#
# Copyright (c) 2025 Patrick Linstruth
# https://github.com/deltecent
#
# Boot SD Systems CP/M 2.2
#

set env AZ80_TEST_NAME=SD Systems CP/M 2.2 Test
set env AZ80_TEST_BIN=sds_cpm2
echof "\r\n*** %SIM_NAME%: %AZ80_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %AZ80_TEST_BIN%.zip curl "https://raw.githubusercontent.com/deltecent/altairz80-packages/main/%AZ80_TEST_BIN%/%AZ80_TEST_BIN%.zip" --output %AZ80_TEST_BIN%.zip
!unzip -o %AZ80_TEST_BIN%.zip '*.dsk'

noexpect
set runlimit 100M instructions
d sleep 0
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

set cpu 64k
set cpu itrap
set cpu z80
set cpu noaltairrom
set hdsk dis
;set debug stdout

; Configure SBC200 and Interrupts
set sbc200 ena
dep SBCRXINTEN0 1
dep SBCRXVEC0 3
dep SBCRXDBVAL0 82

; Configure disk controller for SD Systems CP/M 2
set vfii en
dep dsmask 80

; Attach CP/M 2.2 disk images
att vfii0 SDS_CPM2_64K.dsk
att vfii1 SDS_CPM2_BLANK.dsk

:SDOS_FLOPPY_TEST
echof "\r\n*** Format Floppy Test (SSDD)"

:SDOS_FLOPPY_SSSD
noexpect
expect "A>"; send "\n"; goto FLPYFMTSSSD
expect "\r\n." ; goto FAIL
reset
g f000
goto FAIL

:FLPYFMTSSSD
call FLPYFORMAT B C	; Format B: SSDD
call FLPYCOPY 52
call SYSGEN1 A B
call REBOOT 01
goto PASS

:FLPYFORMAT
; Format floppy <drive> <density> <sided>
noexpect
expect "A>" ; send "FORMAT\n"; go -q
expect "ENTER DRIVE TO BE FORMATTED " ; send "%1\n"; go -q
expect "DISKETTE TYPE: " ; send "%2" ; go -q
expect "FORMAT FINISHED" ; send "\r" ; go -q
expect "A>" ; send "\003"; return
go -q
goto FAIL

:FLPYCOPY
noexpect
expect "A>" ; send "PIP B:=A:*.*\r" ; go -q
expect "A>" ; send "STAT B:\r" ; go -q
expect "B: %1k" ; go -q
expect "A>" ; send "\r" ; return
go -q
goto FAIL

:SYSGEN1
noexpect
expect "A>" ; send "SYSGEN\r" ; go -q
expect "SOURCE DRIVE NAME"; send "%1\r" ; go -q
expect "FUNCTION COMPLETE" ; goto SYSGEN2
go -q
goto FAIL

:SYSGEN2
noexpect
expect "DESTINATION DRIVE NAME" ; send "%2\r" ; go -q
expect "FUNCTION COMPLETE" ; send "\r" ; go -q
expect "A>" ; return
go -q
goto FAIL

:REBOOT
noexpect
expect "\r\n." ; send "C%1\r"; go -q
expect "A>" ; return
reset cpu
reset sbc200
g e000
go -q
goto FAIL

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %AZ80_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach vfii0
detach vfii1

set vfii disabled
set -f sbc200 disabled

# Set wd179x iobase back to default and disable.
set wd179x iobase=30
set wd179x disabled

# Remove disk images
del SDS_CPM2_64K.dsk
del SDS_CPM2_BLANK.dsk
del sds_cpm2.zip

echof "\n\r*** PASSED - %SIM_NAME%: %AZ80_TEST_NAME%.\n"

:DONE
