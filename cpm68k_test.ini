# CP/M-68K test for SIMH/AltairZ80 with 68000 CPU
#
# Copyright (c) 2022 Howard M. Harte.
# https://github.com/hharte
#
# Reassemble loader and CPM.SYS and make sure the system can boot.
#

set env AZ80_TEST_NAME=CP/M-68K Test
set env AZ80_TEST_BIN=cpm68k
echof "\r\n*** %SIM_NAME%: %AZ80_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"
if not exist %AZ80_TEST_BIN%.zip curl "https://schorn.ch/cpm/zip/%AZ80_TEST_BIN%.zip" --output %AZ80_TEST_BIN%.zip
!unzip -o %AZ80_TEST_BIN%.zip diskc.dsk

noexpect
set runlimit 165M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

; Boot CP/M-68K from disk C:
set cpu m68k
; Disk C:
set hdsk en
attach hdsk2 diskc.dsk

# Messages that indicate failure in various utilities.
expect "Exception" goto FAIL
expect "not object file" goto FAIL
expect "not archive format" goto FAIL
expect "read error on file" goto FAIL
expect "Cannot open" goto FAIL

# Build CPM.SYS and Loader
expect "\r\nC>" send "era cpm.sys\n"; go -q
expect "\r\nC>" send "user 4\n"; go -q
expect "\r\n4C>" send "era ldrif.o\n"; go -q
expect "\r\n4C>" send "era bdosif.o\n"; go -q
expect "\r\n4C>" send "era bdosmain.o\n"; go -q
expect "\r\n4C>" send "era bdosread.o\n"; go -q
expect "\r\n4C>" send "era cpmldr.o\n"; go -q
expect "\r\n4C>" send "era dskutil.o\n"; go -q
expect "\r\n4C>" send "era fileio.o\n"; go -q
expect "\r\n4C>" send "era iosys.o\n"; go -q
expect "\r\n4C>" send "submit m\n"; go -q
expect "IOSYS.O \r\n\r\n4C>" send "submit make c: c:\n"; go -q
expect "\r\n2C>" send "era simbios.o\n"; go -q
expect "\r\n2C>" send "era cpm.sys\n"; go -q
expect "\r\n2C>" send "era cpm.rel\n"; go -q
expect "\r\n2C>" send "era ldrlib\n"; go -q
expect "\r\n2C>" send "era ldrbios.o\n"; go -q
expect "\r\n2C>" send "era cpmldr.rel\n"; go -q
expect "\r\n2C>" send "era cpmldr.sys\n"; go -q
expect "\r\n2C>" send "pip c:=ldrlib[g4]\n"; go -q
expect "\r\n2C>" send "submit makecpm\n"; go -q
expect "CPM.SYS\r\n\r\n2C>" send "submit makeldr\n"; go -q
expect "CPMLDR.SYS C\r\n\r\n2C>" send "user 0\n"; go -q
expect "user 0\r\r\n\r\nC>" send "pip c:=???.SYS[g2v]\n"; go -q
expect "YS\r\n\r\nC>" send "stat c:\n"; go -q
expect "K\r\nC>" send "whereis cpm.sys\n"; go until "C>"

echof "\r\n*** Building CPM.SYS and loader..."
boot hdsk2

# Check whether the newly generated CPM.SYS and loader work
# by rebooting CP/M-68K.
noexpect
echof "\n\r*** CPM.SYS and Loader generated, rebooting."
expect "\r\nC>" goto PASS
expect "\r\nOpen or Read error on CPM.SYS" goto FAIL
reset
boot hdsk2

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %AZ80_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
detach hdsk2
set hdsk disabled
rm diskc.dsk
echof "\n\r*** PASSED - %SIM_NAME%: %AZ80_TEST_NAME%.\n"
