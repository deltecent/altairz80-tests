# SIMH/AltairZ80 Test for CompuPro 68000 CP/M-68K v1.1
#
# Copyright (c) 2022 Howard M. Harte.
# https://github.com/hharte
#
# This installs CompuPro CP/M-68K v1.1 to the hard disk from System
# Master disks
#
# * Assemble CompuPro CPMLDR.SYS
# * Assemble CompuPro utilities
# * Compile SIMH read and write utilities.
# * Build a four-disk CP/M-68K v1.3 System Master disk set.
#   BIOS supports four 8" floppies, A-D, and a 20MB hard disk E.
# * Disk 1 boots on the 68000 CPU, disk 2 boots on the 68010.
#
# Reference:
# https://bitsavers.org/pdf/compupro/CPM_68K_Technical_Manual_and_Installation_Feb84.pdf
#

set env AZ80_TEST_NAME=CompuPro CP/M-68K v1.1 System Master Disk Installation
set env AZ80_TEST_BIN=ccpm68k11_test
echof "\r\n*** %SIM_NAME%: %AZ80_TEST_NAME% starting:"
echof "\r\n*** Setting initial conditions"

if not exist CompuPro_68K_System_Masters.zip curl "https://bitsavers.org/bits/Compupro/CompuPro_68K_System_Masters.zip" --output CompuPro_68K_System_Masters.zip
!unzip -o CompuPro_68K_System_Masters.zip

if not exist %AZ80_TEST_BIN%.zip curl "https://raw.githubusercontent.com/hharte/altairz80-packages/main/ccpm68k11_test/%AZ80_TEST_BIN%.zip" --output %AZ80_TEST_BIN%.zip
!unzip -o %AZ80_TEST_BIN%.zip

noexpect
set runlimit 465M instructions
set on
on error ignore
on runtime echof "\r\n*** Test Runtime Limit %SIM_RUNLIMIT% %SIM_RUNLIMIT_UNITS% Exceeded ***\n"; goto FAIL

#set debug stdout
set hdsk disable

set cpu m68k
reset cpu

set sio ansi
set sio port=5c/0/0/0/0/f/0/t
set sio port=5d/0/2/0/81/f/0/f

# Configure floppy disks, A-D
set i8272 enabled
set disk1a enabled
set disk1a nodebug
att disk1a0 CompuPro_68K_System_Master_Disk_1_of_3.imd
att disk1a1 CompuPro_68K_System_Master_Disk_2_of_3.imd
att disk1a2 CompuPro_68K_System_Master_Disk_3_of_3.imd
att disk1a3 ccpm68k11_d1.imd

set disk1a0 norom

# The controller ROM is not accessible to the M68K CPU because
# the MDEV / mmu_table is not implemented for M68K.  Instead,
# load the CompuPro 68K bootstrap from a file to low RAM.
# Go at address 0xC to boot.
#d disk1a bootstrap 8
load CPRO68K.BIN 0

# System Support 1 Board
set ss1 enabled

# CompuPro DISK2 subsystem
# Fujitsu 20M: Drives E,F,G
set selchan enabled
set disk2 enabled
att disk20 ccpm68k11_20mb.dsk

# CompuPro MDRIVE-H
set mdriveh enabled
set mdriveh1 enabled
set mdriveh2 enabled
set mdriveh3 enabled
set mdriveh4 enabled
set mdriveh5 enabled
set mdriveh6 enabled
set mdriveh7 enabled

d sleep 0

# Messages that indicate failure in various utilities.
expect "Exception" goto FAIL
expect "not object file" goto FAIL
expect "not archive format" goto FAIL
expect "read error on file" goto FAIL
expect "Cannot open" goto FAIL
expect "Open or Read error on" goto FAIL
expect "ERROR:" goto FAIL

expect "\r\nA>" send "PIP D:=A:CPMLIB\n"; go -q
expect "\r\nA>" send "PIP D:=A:CPM.REL\n"; go -q
expect "\r\nA>" send "PIP D:=B:AS.68K\n"; go -q
expect "\r\nA>" send "PIP D:=B:LO.68K\n"; go -q
expect "\r\nA>" send "PIP D:=B:RELOC.68K\n"; go -q
expect "\r\nA>" send "PIP D:=B:AS68SYMB.DAT\n"; go -q
expect "\r\nA>" send "D:\n"; go -q
expect "\r\nD>" send "B:MAKESYS\n"; go -q
expect "ERA BIOS.O\r\n\r\nD>" goto DORC

# Execute the DISK1A bootstrap from RAM.
g c
goto FAIL

:DORC
expect "D>ERA CPM.REL\r\n\r\nD>" goto PUTBOOT
send "B:RC D\n"
go -q
goto FAIL

:PUTBOOT
expect "\r\n\r\nD>" goto REBOOT
send "A:PUTBOOT A:CPMLDR.SYS D\n"
go -q
goto FAIL

:REBOOT
detach disk1a0
detach disk1a1
detach disk1a2
detach disk1a3
reset cpu
load CPRO68K.BIN 0
att disk1a0 ccpm68k11_d1.imd
att disk1a1 CompuPro_68K_System_Master_Disk_1_of_3.imd
att disk1a2 CompuPro_68K_System_Master_Disk_2_of_3.imd
att disk1a3 CompuPro_68K_System_Master_Disk_3_of_3.imd

expect "\r\nA>" send "B:DISK2 FORMAT DATA\n"; go -q
expect "\r\nA>" send "B:PIP E:=B:*.*\n"; go -q
expect "\r\nA>" send "B:PIP E:=C:*.*\n"; go -q
expect "\r\nA>" send "B:PIP E:=D:*.*\n"; go -q
expect "\r\nA>" send "B:PIP E:=A:*.*\n"; go -q
expect "\r\nA>" send "E:\n"; go -q
expect "\r\nE>" send "STAT E:*.*\n"; go -q
expect "E: RW, FREE SPACE:" goto PASS

go c

# All test failures wind up here.
:FAIL
echof "\n\r*** FAILED - %SIM_NAME%: %AZ80_TEST_NAME%.\n"
exit 1

# Test passed.
:PASS
echof "\n\rTests succeeded, generating package"
del ccpm68k11.zip
!zip ccpm68k11.zip ccpm68k11_d1.imd ccpm68k11.ini CPRO68K.BIN ccpm68k11_20mb.dsk README.TXT
echof "\n\r*** PASSED - %SIM_NAME%: %AZ80_TEST_NAME%.\n"

:DONE
detach disk1a0
detach disk1a1
detach disk1a2
detach disk1a3
detach disk20

:FAILW